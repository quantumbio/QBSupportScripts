#svl

#if 0
 // BEGIN COPYRIGHT
 /***********************************************************************
    Copyright (c) 2021 QuantumBio Inc. and/or its affiliates.
	
 This source code is the property of QuantumBio Inc. and/or its affiliates
 and is provided AS IS.

 This source code may contain proprietary and Confidential Information, 
 including trade secrets, belonging to QuantumBio Inc. and/or its 
 affiliates.

 Please see http://www.quantumbioinc.com/ for more information.

 ***********************************************************************/
 // END COPYRIGHT
#endif      


global argv;

function PartialCharge;
function ArgvPull;
function fwrite_TriposMOL2;
function fwrite_PDB;
function db_ImportSD;
function db_ExportTriposMOL2;

local function main []
    ArgvReset ArgvExpand argv;

    local [complex,ligfile,posefile,ligRes,ligUID,ligChain,setLigName] = ArgvPull [ ['-complex','-ligfile','-posefile','-ligRes','-ligUID','-ligChain','-setLigName'], [1,1,1,1,1,1,1] ];
    
        local remaining = ArgvAll [];
    if not isnull remaining then
        exit twrite ['ERROR: Unknown command line options: {}\n',remaining];
    endif
    
    if isnull complex then
        exit twrite ['ERROR: use -complex to supply a PDB or mol2 file with the protein:ligand complex or receptor.'];
    endif

    ReadAuto complex;
    local infileBasename = fbase ftail complex;
    
    if isnull ligfile then

        if isnull ligRes then
            exit twrite ['ERROR: use -ligRes to supply the 3letter code for the ligand in the protein:ligand complex.'];
        endif
    
        if '' == first fstat complex then
            exit twrite ['ERROR: file {} not found!', complex];
        endif

        if isnull ligChain then
            ligChain = cName rChain first (Residues [] | rName Residues[] == ligRes);
            write ['WARNING: use -ligChain to supply the Chain ID for the ligand {}. Assuming first copy found in chain {}.\n', ligRes, ligChain];
        else
            ligChain = twrite ['{}.{}',infileBasename, ligChain];
        endif

        if isnull ligUID then
            ligUID = rUID first ( (cat cResidues (Chains[] | cName Chains[] == ligChain)) | (rName (cat cResidues (Chains[] | cName Chains[] == ligChain)) == ligRes));
            write ['WARNING: use -ligUID to supply the UID/numerical code for the ligand {}. Assuming first copy found in chain {}: {}\n', ligRes, ligChain, ligUID];
        else
            ligUID = atoi ligUID;
        endif
    else
        ligChain = ReadAuto ligfile;
        ligRes = cResidues ligChain;
        ligUID = rUID ligRes;
        ligChain = cName ligChain;
        ligRes = rName ligRes;
    endif
    
    local selection = Residues [] | (rUID Residues[] == ligUID and cName rChain Residues [] == ligChain );
    
    if isnull selection then
        exit twrite ['ERROR: ligand {} {} {} NOT FOUND in {}', ligRes, ligUID, ligChain, complex];
    endif
    
    if length selection > 1 then
        exit twrite ['ERROR: ligand {} {} {} has more than one match in {}', ligRes, ligUID, ligChain, complex];
    endif
    
    if not isnull setLigName then
        rSetName [selection, setLigName];
    endif

    write ['Ligand: {} | {} | {}\n',cName rChain selection, rName selection, rUID selection];
    
    pot_Load '$MOE/lib/Amber10EHT.ff';

    // Note that the following is the general potential set up only.
    // Individual stages like refinement may use a modified set up.
    pot_Setup [
        strEnable: 1,
        angEnable: 1,
        stbEnable: 1,
        oopEnable: 1,
        torEnable: 1,
        vdwEnable: 1,
        eleEnable: 1,
        solEnable: 0,
        resEnable: 1,
        strWeight: 1,
        angWeight: 1,
        stbWeight: 1,
        oopWeight: 1,
        torWeight: 1,
        vdwWeight: 1,
        eleWeight: 1,
        solWeight: 1,
        resWeight: 1,
        cutoffEnable: 1,
        cutoffOn: 8,
        cutoffOff: 10,
        eleDist: 2,
        vdwScale14: 0.5,
        vdwBuffer1: 0,
        vdwBuffer2: 0,
        eleScale14: 0.833333,
        eleDielectric: 1,
        eleBuffer: 0,
        solDielectric: 80,
        solDielectricOffset: 0,
        state0: 1,
        state1: 0,
        state2: 1,
        threadCount: 0
    ];
    pot_SetCharges [];

    function writeFile [ofilename, residues]
        write ['NOTE: Writing {} residues to {}\n', length residues, ofilename];
        if fext ofilename == 'mol2' then
            fwrite_TriposMOL2   [ofilename, residues];
        elseif fext ofilename == 'pdb' then
            fwrite_PDB   [ofilename, residues];
        endif
    endfunction

    writeFile   [twrite ['{}-cmplx.mol2',infileBasename], Residues[]];
    writeFile   [twrite ['{}-targ.mol2',infileBasename], diff [Residues[],selection]];
    writeFile   [twrite ['{}-lig.mol2',infileBasename], selection];
    writeFile   [twrite ['{}-cmplx.pdb',infileBasename], Residues[]];
    writeFile   [twrite ['{}-targ.pdb',infileBasename], diff [Residues[],selection]];
    writeFile   [twrite ['{}-lig.pdb',infileBasename], selection];
    
    if not isnull posefile then
        // process PoseFile through use of temp MDB
        local tmpMDB = 'DELME-1234567890.mdb';
        fdelete tmpMDB;
        local mdb_key = db_Open [ tmpMDB,'create' ];
        db_Close mdb_key;
        db_ImportSD [tmpMDB, posefile,'mol'];
        db_EnsureField [tmpMDB, 'Complex', 'molecule'];
        local ent = 0;
        local inEntry;
        local pdbHandle = fopenw twrite ['{}-cmplxes.pdb',infileBasename];
        while ent = db_NextEntry [tmpMDB,ent] loop
            local psys = SystemPush diff [Residues[],selection];
            inEntry = db_Read [ tmpMDB, ent];
            ligRes = cat cResidues mol_Create inEntry.mol;
            if not isnull setLigName then
                rSetName [ligRes, setLigName];
            endif
           if rName ligRes == '*' then
                rSetName [ligRes, 'MOL'];
            endif                
            RenderProtein [Chains[]];
            inEntry.Complex = moe_CreateMOL _moe_Extract [Atoms[], GKeyList[], [mol_name: first cTag Chains[]]];
            local tmpKey = db_Write [ tmpMDB, ent, inEntry];
            fwrite_PDB [pdbHandle, Chains[]];
            SystemPop psys;
        endloop
        fclose pdbHandle;
        db_ExportTriposMOL2 [tmpMDB, twrite ['{}-cmplxes.mol2',infileBasename], 'Complex'];
        fdelete tmpMDB;
    endif

endfunction
